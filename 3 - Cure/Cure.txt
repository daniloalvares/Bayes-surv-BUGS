model {
  for(i in 1:n) {
     # Logistic regression model (cured subpopulation)
     logit(eta[i]) <- inprod(betaC[], XC[i, ])
  
     # PH model (uncured subpopulation)
     # Weibull baseline
     base[i] <- lambda * alpha * pow(t[i], alpha - 1)
     elinpred[i] <- exp(inprod(betaU[], XU[i, ]))
     # Log-hazard function
     logHaz[i] <- log(base[i] * elinpred[i])
     # Log-survival function
     logSurv[i] <- -lambda * pow(t[i], alpha) * elinpred[i]
  
     # Definition of the log-likelihood using zeros trick
     logLike[i] <- delta[i] * (log(1 - eta[i]) + logHaz[i] + logSurv[i]) + 
       (1 - delta[i]) * log(eta[i] + (1 - eta[i]) * exp(logSurv[i]))
     phi[i] <- 100000 - logLike[i]
     zeros[i] ~ dpois(phi[i])
  }

  # Prior distributions 
  for(l in 1:NbetasC) {
    betaC[l] ~ dnorm(0, 0.001)
  }
  for(l in 1:NbetasU) {
    betaU[l] ~ dnorm(0, 0.001)
  }
  lambda ~ dgamma(0.01, 0.01)
  alpha ~ dunif(0, 10)
}
