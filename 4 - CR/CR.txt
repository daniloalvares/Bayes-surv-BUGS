model {
  for(i in 1:n) {
    for(k in 1:Nrisks) {
       # Weibull baseline
       base[i, k] <- lambda[k] * alpha[k] * pow(t[i], alpha[k] - 1)
       elinpred[i, k] <- exp(inprod(beta[, k], X[i, ]))
       # Log-hazard functions
       logHaz[i, k] <- log(base[i, k] * elinpred[i, k])
       # Log-survival functions
       logSurv[i, k] <- -lambda[k] * pow(t[i], alpha[k]) * elinpred[i, k]
  }

  # Definition of the log-likelihood using zeros trick
  phi[i] <- 100000 - inprod(delta[i, ], logHaz[i, ]) - sum(logSurv[i, ])
  zeros[i] ~ dpois(phi[i])
  }

  # Prior distributions
  for(k in 1:Nrisks) {
    for(l in 1:Nbetas) {
      beta[l, k] ~ dnorm(0, 0.001)
    }
    lambda[k] ~ dgamma(0.01, 0.01)
    alpha[k] ~ dunif(0, 10)
  }
}
