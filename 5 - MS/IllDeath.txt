model {
  for(i in 1:n) {
     # Linear predictor
     elinpred[i, 1] <- exp(inprod(beta[, 1], X[i, ]))
     elinpred[i, 2] <- exp(inprod(beta[, 2], X[i, ]))
     elinpred[i, 3] <- exp(inprod(beta[, 3], X[i, ]))
     # Log-hazard functions
     logHaz[i, 1] <- log(lambda[1] * alpha[1] * pow(t1[i], alpha[1] - 1) * elinpred[i, 1])
     logHaz[i, 2] <- log(lambda[2] * alpha[2] * pow(t2[i], alpha[2] - 1) * elinpred[i, 2])
     logHaz[i, 3] <- log(lambda[3] * alpha[3] * pow(t3[i], alpha[3] - 1) * elinpred[i, 3])
     # Log-survival functions
     logSurv[i, 1] <- -lambda[1] * pow(t1[i], alpha[1]) * elinpred[i, 1]
     logSurv[i, 2] <- -lambda[2] * pow(t2[i], alpha[2]) * elinpred[i, 2]
     logSurv[i, 3] <- -lambda[3] * pow(t3[i], alpha[3]) * elinpred[i, 3]

     # Definition of the log-likelihood using zeros trick
     phi[i] <- 100000 - inprod(event[i, ], logHaz[i, ]) - sum(logSurv[i, ])
     zeros[i] ~ dpois(phi[i])
  }

  # Prior distributions
  for(k in 1:3) {
    for(l in 1:Nbetas) {
      beta[l,k] ~ dnorm(0, 0.001)
    }
    lambda[k] ~ dgamma(0.01, 0.01)
    alpha[k] ~ dunif(0, 10)
  }
}
